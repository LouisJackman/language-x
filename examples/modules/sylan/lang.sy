package
/**
 * This is the proposed prelude file, which serves as an example of what Sylan
 * should look like and also shows what utilities are available everywhere.
 *
 * This is similar in spirit to Java's `java.lang` package or Haskell's
 * standard prelude. Eventually it should also expose built-ins via
 * declarations without bodies, once the mechanics of that have been worked out.
 *
 * For now, we just `extend` existing built-in and types and mark them as
 * extern, which is essentially a placeholder until how built-in types are
 * fully worked out.
 */
(
    sylan {
        core.intrinsics.concurrent.atomic.AtomicMachineType,
        base {
            util {
                collections.tree as treeCollection,
                exceptions,
            },
            runtime.memory as memoryRuntime,
    },
    this.package.unsafe.atomic.mutable as atomicMutable,
)

class public Void
/**
 * The unit-type, a.k.a. `void`. It can be instantiated, but contains
 * nothing.
 */

enum public Boolean @derive(Equals, ToString) (
    public True,
    public False,
) {
    fun public operator && (other This) extern
    fun public operator || (other This) extern
    fun public negate() extern
}

class public True = Boolean.True
class public False = Boolean.False

fun public not(_ b Boolean) Boolean {
    b.negate
}

class ValueMissing implements Exception {

    var fun public override message String {
        "value missing"
    }
}

enum public Optional[of Value](
    Some(Value),
    public Empty,
)(of present Value) {

    { .Some(present) }

    fun public map(_ f (x Value) Value) This {
        if var .Some(x) = this {
            x |> f |> Optional(of: _)
        } else {
            None
        }
    }

    fun public flatMap(_ f (x Value) This) This {
        if var .Some(x) = this {
            if var .Some(y) = f(x) {
                Optional(of: y)
            } else {
                Empty
            }
        } else {
            Empty
        }
    }

    fun public filter(_ p (x Value) Bool) This {
        flatMap -> {
            if p(it) {
                Optional(of: it)
            } else {
                Empty
            }
        }
    }

    fun public orElse(_ newAlternative () Value) Value {
        if .Some(x) = this {
            x
        } else {
            newAlternative()
        }
    }

    fun public or(_ alternative Value) Value {
        orElse(-> { alternative })
    }

    fun public orElseThrow() Value {
        orElse(-> { throw valueMissing() })
    }
}

enum public Throws[_ Exception extends Exception: Exception, otherwise Result: Void](
    public Ok(Result)
    public Exception(Exception)
)

fun public ok[E extends Exception: Exception]() Throws[E, otherwise: Void] {
    Throws.Ok(Void)
}

fun public final if
    /**
     * A hypothetical look at how `if` could be defined as a macro, creating
     * Sylan _from_ Kernel Sylan rather than translating _to_ Kernel Sylan in
     * Rust.
     *
     * This example is a bit silly because it assumes simplification steps like
     * applicative do-notation, pattern-matching, and `var` are already defined.
     */
(syntax pipeline Pipeline[from: CharReader, to: AstWriter]) Throws {

    var Pipeline(source, write) = pipeline

    // Trigger characters for read macros (and the trigger token for a
    // procedural macro) are available on `source.trigger` but are no longer
    // present in the source.

    // Explicit lexing triggers reader macros for characters consumed. They
    // aren't given another opportunity to trigger again in the generated
    // source.
    var lexer = Lexer(lexing: source)

    // Explicit parsing triggers procedural macros for tokens consumed. They
    // aren't given another opportunity to trigger again in the generated
    // source.
    //
    // Item macros can be both procedural and pattern, so the procedural kind
    // are also processed here.
    var parser = Parser(parsing: lexer)

    // At this point, only pattern macros remain unprocessed in the
    // generated source.

    with {
        var condition = parser.parseExpression()?
        var ifTrue = parser.parseScope()?
        parser.expectAndDiscard(tokens.Token.BranchingAndJumping(tokens.BranchingAndJumping.Else))?
        var ifFalse = parser.parseScope()?

        var ast = quote {
            switch unquote condition {
                True unquote ifTrue
                False unquote ifFalse
            }
        }
        write(ast)?
    }

    ok()
}

// Wire the characters "if" to the current read table for the rest of the
read > if readIf
read <

fun final kernelIf
    /**
     * Now, a second look at how this would look in Kernel Sylan. This is done
     * from a procedural macro rather than a reader one to keep the example
     * small.
     *
     * Even this assumes that lexical scoping is already defined though, but it
     * assumes that non-field `var`, pattern matching, and `with` do not yet
     * exist.
     *
     * `switch` is a special form, so it can be relied on here. The raw switch
     * doesn't do pattern matching or guards though.
     */
(syntax pipeline Pipeline[from: TokenReader, to: AstWriter]) Throws {
    -> source, write {
        -> parser {
            parser.parseExpression().flatMap(-> condition {
                parser.parseScope().flatMap(-> ifTrue {
                    parser.expectAndDiscard(tokens.Token.BranchingAndJumping(tokens.BranchingAndJumping.Else)).flatMap(-> _ {
                        parser.parseScope().flatMap(-> ifFalse {
                            write(quote {
                                switch unquote condition {
                                    True unquote ifTrue
                                    False unquote ifFalse
                                }
                            })
                        })
                    })
                })
            })
        }(Parser(parsing: source))
    }(pipeline.source, pipeline.dest)

    ok()
}

class public Invokable[Result, Args..] extern 

extend class Invokable[Result, Args..] {

    fun public operator |> [T](other (Result) T) T extern 
    fun public operator ~ [T](other (Result) T) (Args..) T extern 
}

interface public Number {

    // Natively-defined arithmetic operators.
    fun public operator + (other This) This extern 
    fun public operator - (other This) This extern 
    fun public operator / (other This) This extern 
    fun public operator % (other This) This extern 
    fun public operator * (other This) This extern 
    fun public operator ** (other This) This extern 

    // Natively-defined bitwise operators.
    fun public operator & (other This) This extern 
    fun public operator | (other This) This extern 
    fun public operator ^ (other This) This extern 

    fun public operator < (other This) This extern 
    fun public operator <= (other This) This extern 
    fun public operator == (other This) This extern 
    fun public operator != (other This) This extern 
    fun public operator > (other This) This extern 
    fun public operator >= (other This) This extern 

    fun public operator << (other This) This extern 
    fun public operator >> (other This) This extern 
    fun public operator >>> (other This) This extern 

    fun public negate
        /**
         * Negate a number; replaces the unary minus operator present in other
         * languages.
         *
         * ```
         * assert(4.negate == (0 - 4))
         * ```
         */
    () This {
        if 0 <= this {
            this - this.double()
        } else {
            this
        }
    }

    fun public double() This {
        this * 2
    }

    fun public triple() This {
        this * 3
    }

    fun public quadruple() This {
        this * 4
    }

    fun public quintuple() This {
        this * 5
    }

    fun public up(to n This) List[of: This] {
        list.generate(initial: 1) -> {
            if n < it {
                list.generation.Finish
            } else {
                list.generation.Next(it, it + 1)
            }
        }
    }

    fun public twice() List[of: This] { up(to: 2) }
    fun public thrice() List[of: This] { up(to: 3) }
}

fun public extern bitnot[N extends Number](n N) N

// Built-in types, which look a bit like Java primitives except that they are
// real OO-style types. These can't be defined in Sylan itself due to their
// precise definition in raw memory.
class public Int extern 
class public UInt extern 
class public Byte extern 
class public UInt16 extern 
class public UInt32 extern 
class public UInt64 extern 
class public Int8 extern 
class public Short extern 
class public Int32 extern 
class public Long extern 
class public Float extern 
class public Double extern 

class public Usize extern 
/**
 * The maximum size of a contiguously allocated slab of memory. This is an
 * important concept in Sylan even outside of unsafe code, e.g. slice indices.
 */

fun final ignore
    /**
     * Useful for throwing away variables in contexts that are not already
     * explicitly void, such as enum instance initialisers and implicily-typed
     * lambdas. It works well with the pipe operator:
     *
     * add(40, 2) |> ignore
     */
[_ Item](_ Item) { }

fun final match[Item](_ item Item, syntax against pipeline AstPipline) Throws {
    var refuttablePattern = pipeline.source

    var ast = quote {
        switch unquote item {
            unquote refuttablePattern { True }
            _                         { False }
        }
    }
    pipeline.write(ast)
}

class public Ref
    /**
     * Sylan is an immutable language; data can't be modified. It's not _fully_
     * immutable though, due to its strong support for processes, or "tasks" as
     * Sylan calls them.
     * 
     * A task can hold its own state and recurse with differing data based
     * on incoming process messages, and send out messages with state changes.
     * 
     * Therefore, a mutable "memory address" can be implemented by a task.
     * However, unlike a raw memory address, the updates are always atomic and
     * always update the _entire_ value in one go, not just _part_ of its memory
     * content.
     */
[to value Value] public (to value Value) implements AutoClosable {

    class Alter = (_ Value) Value
    enum Msg(Alter(with alter Alter, parent Task), Stop)

    fun public sealOff() { send(.Stop, to: task) }
    fun public override close = sealOff

    fun public alter(with alter Alter) Value {
        sendAndWait(.AlterMsg(alter, self), to: task)
    }

    var fun public value Value                 { alter(id) }
    fun public swap(with newValue Value) Value { alter(-> _ { newValue }) }
    fun public set(to newValue Value)          { swap(with: newValue) |> ignore }

    var task = spawn -> {
        for var state = value {
            select Msg {
                .Alter(alter, parent) {
                    var Msg(alter, parent) = select Msg
                    send(state, to: parent)
                    continue(newState)
                }
                .Stop { }
            }
        }
    }
}

interface ForEach[_ Value] {
    fun each(_ (Value))
}

class public Vector[of Item](of items Item..) implements AbstractVector, ForEach {

    // This works because variadics use a concrete implementation of
    // `AbstractVector`, `IntrinsicVector`, to instantiate themselves directly
    // as an intrinsic.
    var raw AbstractVector = items
    
    fun public operator [||] = raw.operator [||]

    fun public override each(_ f (Value)) {
        var size = size.value
        
        for var i = 0 {
            var value = this[|i|].orElseThrow()
            f(value)
            if (i + 1) < size {
                continue(i + 1)
            }
        }
    }
}

enum public List[of Item](
    public Nil,
    public Element(of item Item, next () This),
)(of items Item..) {

    {
        for var remaining = items {
            if var .Some(element) = remaining[|0|] {
                Node.Element(of: element) -> {
                    remaining[|1 :]
                        |> continue
                }
            } else {
                Node.Nil
            }
        }
    }

    var public isEmpty Boolean = match(node, against: Nil)

    var public first Optional[of: Item] = switch node {
        Element(item, ..) { Optional(of: item) }
        Nil               { Empty }
    }

    var public rest Optional[of: This] = switch node {
        Element(_, next) { Optional(of: next) }
        Nil              { Empty }
    }
}

interface public ToString {
    fun public toString() String
}

interface public String {
    //
}

class public TaintedString implements String {
    //
}

class public EscapedString implements String {
    //
}

class public CompileTimeString implements String {
    //
}

class public Decimal {
    //
}

interface LanguageReader[reading Item] {

    var fun trigger List[Item]

    fun public readMany
        /** Read from the reader, consuming them as they are returned. */
    (
        amount Usize
        /** The amount to attempt to read. */
    )
        Throws[otherwise: List[of: Item]]
        /**
         * What was read; if smaller than the amount, it means the reader hit
         * the end of its source. This means that the end of the source can be
         * detected by requesting more than zero to read and checking for an
         * empty resulting list.
         *
         * That is what [is_finished] does.
         */

    fun public peekMany
        /** Peek in the reader, multiple characters from an index. */
    (
        amount Usize,
        /** The amount to attempt to peek at.  */

        from index Usize: 0
        /** Whence to peek onwards. */
    )
        Throws[otherwise: List[of: Item]]
        /**
         * What was read; if smaller than the amount, it means the reader hit
         * the end of its source. This means that the end of the source can be
         * detected by requesting more than zero to peek at and checking for an
         * empty resulting list.
         *
         * That is what [is_finished] does.
         */

    fun public discard
    (
        amount Usize: 1
        /** The amount to attempt to discard. */
    )
        Throws[otherwise: Boolean]
        /**
         * Whether all `amount` items were discarded before encoutering the end
         * of the stream.
         */

    fun public read() Throws[otherwise: Optional[Item]] {
        readMany(amount: 1)
            .map(:first)
    }

    fun public peek(at index: 0) Throws[otherwise: Optional[Item]] {
        peekMany(amount: 1, from: index)
            .map(:first)
    }

    var fun public isFinished Throws[otherwise: Boolean] {
        peek.map(:isEmpty)
    }

    fun public skipUntil
        /**
         * Consume and discard _until_ an item passes the match, so it's the
         * next item in the stream for the caller.
         */
    (matches (desired Item) Boolean)
        Throws[otherwise: Boolean]
        /**
         * Whether the desired item was encountered before the end of the
         * stream.
         */
    {
        with {
            switch peek? {
                .Some(item) if matches(desired) { Ok(True) }
                .Some(_)                        { skipUntil(matches) }
                Empty                          { Ok(False) }
            }
        }
    }

    fun public skip
        /**
         * Consume and discard _until_ the item, so it's the next item in the
         * stream for the caller.
         */
    (until item Item)
        Throws[otherwise: Boolean]
        /** Whether the desired item was encountered before the end of the stream. */
    {
        skipUntil(matches: _ == item)
    }
}

interface public Ast {
    //
}

interface public ParameterAst extends Ast {
    //
}

interface public Token {
    //
}

enum public TokenTree
    /**
     * Tokens trees are just tokens that group together in the case of grouping
     * delimiters like `(`, `{`, and `[`.
     *
     * Sylan's own lexer and parser doesn't use these, they are solely for
     * procedural macros written in Sylan.
     */
(
    public Group(Token.Grouping),
    public Scalar(Token),
)

interface public ParameterLanguageReader[reading Item] extends LanguageReader[reading: Item] {
    var fun public nextIsSubitemSeperator Throws[otherwise: Boolean]
    var fun public nextIsCloseCall Throws[otherwise: Boolean]

    var fun public resemblesEnd Throws[otherwise: Boolean] {
        with {
            (nextIsSubitemSeperator? || nextIsCloseCall?)
                |> Ok
        }
    }
}

interface public TokenReader extends LanguageReader[reading: TokenTree]

interface public ParameterTokenReader extends ParameterLanguageReader[reading: TokenTree] {

    var fun public nextIsSubitemSeperator Throws[otherwise: Boolean] {
        with {
            peek?
                .filter(_ == TokenTree.Scalar(Token.SubItemSeperator))
                .hasValue
                |> Ok
        }
    }

    var fun public nextIsCloseCall Throws[otherwise: Boolean] {
        with {
            peek?
                .filter(_ == (
                    tokens.Grouping.CloseParentheses
                    |> Token.Grouping
                    |> TokenTree.Group
                ))
                .hasValue
                |> Ok
        }
    }
}

interface public CharReader extends LanguageReader[reading: Char]

interface public ParameterCharReader extends ParameterLanguageReader[reading: Char] {

    var fun public nextIsSubitemSeperator Throws[otherwise: Boolean] {
        with {
            peek?
                .filter(_ == ',')
                .hasValue
                |> Ok
        }
    }

    var fun public nextIsCloseCall Throws[otherwise: Boolean] {
        with {
            peek?
                .filter(_ == ')')
                .hasValue
                |> Ok
        }
    }

    fun skipWhitespace
        /**
         * Consume and discard whitespace.
         * 
         * Unlike token reader macros, character reader macros don't get the
         * benefit of being able to ignore whitespace-handling. In fact, the
         * language they're defining might be whitespace-sensitive.
         *
         * This method should help.
         */
    () Throws {
        if skipUntil(matches: Character.isWhitespace ~ not) {
            with {
                discard()?
                skipWhitespace()?
            }
        }
    }
}

class LanguageWriter
    /**
     * Class aliases usually point to other classes. There is one exception:
     * function types can also be pointed to directly.
     */
    [writing Item] = (_ toWrite Item)

interface public CharWriter extends LanguageWriter[writing: Char]
interface public TokenWriter extends LanguageWriter[writing: TokenTree]
interface public AstWriter extends LanguageWriter[writing: Ast]

interface ReaderWriterPipeline[
    of Item,
    readingFrom Reader extends LanguageReader[reading: Item],
    writingTo Writer extends LanguageWriter[writing: Item],
] {
    var fun public source Reader
    var fun public write Writer

    fun public passthroughMany
        /** Pass items through from the source to the destination. */
    (
        amount USize: 1
        /** The amount to passthrough. */
    )
        Throws[otherwise: List[of: Item]]
        /**
         * What was passed through; if smaller than the amount, it means the
         * reader hit the end of its source.
         */
    {
        with {
            var items = source.read(amount)?
            items.each(-> { write(it)? })
            items
        }
    }

    fun public passthrough() Throws[otherwise: Optional[Item]] {
        passthroughMany.map(:first)
    }

    fun passthroughUntil
        /**
         * Many readers won't want to totally discard the whole previous language
         * (mostly likely Sylan itself for single-macro pipelines). They can let
         * most of the language pass through until it sees something it is interested
         * in changing.
         *
         * It skips _until_ an item passes the match, so it's the next item in the stream
         * for the caller.
         */
    (matches (desired Item) Boolean)
        Throws[otherwise: Boolean]
        /** Whether the desired item was encountered before the end of the stream. */
    {
        with {
            switch passthrough? {
                .Some(item) if matches(desired) { Ok(True) }
                .Some(_)                        { passthroughUntil(matches) }
                Empty                           { Ok(False) }
            }
        }
    }

    fun passthroughUntilMatches
        /**
         * Many readers won't want to totally discard the whole previous language
         * (mostly likely Sylan itself for single-macro pipelines). They can let
         * most of the language pass through until it sees something it is interested
         * in changing.
         *
         * It skips _until_ the item, so it's the next item in the stream for the
         * caller.
         */
    (_ item Item)
        Throws[otherwise: Boolean]
        /** Whether the desired item was encountered before the end of the stream. */
    {
        passthroughUntil(matches: _ == item)
    }

    fun passthroughWhitespace
        /**
         * Many readers won't want to totally discard the whole previous language
         * (mostly likely Sylan itself for single-macro pipelines). They can let
         * most of the language pass through until it sees something it is
         * interested in changing.
         *
         * It skips _until_ non-whitespace, so it's the next item in the stream
         * for the caller.
         */
    (_ item Item)
        Throws[otherwise: Boolean]
        /** Whether the desired item was encountered before the end of the stream. */
    {
        passthroughUntil(matches: :isWhitespace ~ not)
    }
}

//
// The following types are used by macros.
//
// ASTs are the easiest to manipulate and the least error prone, but they're
// less powerful and can't be processed lazily and in parallel. They're ideal
// for simplification stages, changing evaluation strategies, creating magic
// identifiers such as anaphoric macros or auto-gensym, but not so much defining
// whole new languages.
//
// Token and character readers are powerful, able to produce any language, but
// also dangerous and unpredictable. Character readers grant the ability to
// define whitespace-sensitive languages.
//
// Unlike Sylan itself, which constrains itself to fixed lookahead to keep the
// language simple and comprehendible, reader and procedural macros can do
// unlimited lookahead with peek for both characters and tokens. Just because
// Sylan is sane enough to avoid unlimited lookahead doesn't mean other
// languages are.
//
// Normal consumers and parameter consumers are mostly the same, but gives the
// macro implementor the ability to change how their language is lexed or parsed
// depending on whether it was triggered by a macro call in another language or
// given a whole file. Parameter consumers also have some helper methods that
// don't make sense for file consumers, such as detecting the end of the
// parameter position that triggered it.
//
// These capabilities are quite "raw", but Sylan libraries can be created to
// raise the abstraction layer. There might be ParSec-style libraries,
// Yacc-style macros, or something else. Sylan is agnostic of the parsing
// strategy you use, providing the building blocks for your preferred approach.
//
// These types are the only valid types for `syntax` parameters. Based on
// which type a macro function defined will change how Sylan presents the
// syntax to that macro and unloads the result back into the destination
// language. A macro either supports file-level or parameter-level.
//
// If a macro wants to support both file- and parameter-level macros, it must
// provide two separate macros for each, otherwise the compiler will complain.
// Of course, one of them can just immediately delegate to the other if the
// sublanguage doesn't change its interpretation from being embedded versus
// being in its own file.
//

class public AstPipeline[Ast extends FileAst: FileAst]
    var public source Ast,
    var public write AstWriter,
)

class public Pipeline[
    of Item,
    readingFrom Source extends LanguageReader[of: Item]: LanguageReader[of: Item],
    writingTo Destination extends LanguageWriter[of: Item]: LanguageWriter[of: Item],
] implements ReaderWriterPipeline[
    of: Item,
    readingFrom: Source,
    writingTo: Destination,
(
    var public source Source,
    var public write Destination,
)

class public AsymmetricPipeline[from Source, to Destination](
    var public source Source,
    var public write Destination,
)

class public Task(var running task ()) {

    fun public start() extern 
}

class public NoAmbientAuthority implements Exception {

    var fun public override toString {
        "this task has no ambient authority"
    }
}

class public AmbientAuthority(var from externTask Task)

fun public spawnTrusted
    /**
     * Spawn, trusting the subprocess with implicit access to the outwide world.
     *
     * For untrusted processes, use `spawn`.
     */
(running run ()) Task {

    var AmbientAuthority(externTask) as auth = select AmbientAuthority {
        _ { it }
        timeout 0.seconds {
            throw NoAmbientAuthority()
        }
    }

    // Refresh inbox in case `spawnTrusted` needs to be run again. Drop it with
    // `dropAmbientAuthority` if not.
    send(auth, to: self)

    spawn(() -> {
        send(auth, to: self)
        run()
    })
}

fun public dropAmbientAuthority() {
    for {
        select AmbientAuthority {
            _                 { continue() }
            timeout 0.seconds { }
        }
    }
}

fun public sendAndWait[sending Item, waitingFor Delivery: Item](
    sending item Item,
    to task Task
) Throws[otherwise: Delivery] {
    send(item, to: task)
        .map(-> Void { select Item })
}

class AssertionError implements Exception {

    var fun public override message String {
        "assertion failed"
    }
}

fun public final assert
    /**
     * Assert that predicate is true, otherwise throw an `AssertionError`.
     */
(syntax that AstPipeline(source, write) AstPipeline) Throws {

    // AST pipelines implement `ToString`. 
    //
    // DO NOT USE THIS TO GENERATE NEW SOURCE TO LEX OR PARSE. Generated ASTs
    // will not expect to trigger reader and procedural macros again, so that'll
    // break all sorts of assumptions of macro writers.
    // 
    // Only use it for converting source code to strings for debugging and
    // tooling purposes.
    var msg = $"Assertion failed: {source}"
    
    var ast = quote {
        if not(unquote source) {
            throw exception.AssertionError(msg)
        }
    }
    write(ast)
}

fun public print(_ message String) extern 

fun public println(_ message String) {
    print(message)
    print("\n")
}

fun public id[of T](of value T) T {
    value
}

fun public constant[of T](of value T) () T {
    -> {
        value
    }
}

// Flip these back to avoid unsafe being default for other packages.
bind unlockUnsafePackage Bool = false
bind allowExtern Bool = false