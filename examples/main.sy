#!/usr/bin/env sylan

/*
 * If no package is specified, "main" is assumed. A `module-info.sy` file is not
 * required for the top level program as there is an inferred `main` module that
 * contains top-level programs.
 */
package main

import sylan.io.{println, print}
import sylan.datetime.ZonedDateTime

void demoIteration() {
    /**
     * SyDocs can be written for code documentation. Unlike JavaDoc but similar
     * to Python, they go inside the item they describe rather than before it.
     */

    List(1, 2, 3).forEach(n ->
        println(`{n}`)
    )

    var highlight = s -> `>> {s} <<`

    1.to(5)
        .map(double # ::toString # highlight)
        .forEach(println)

    var quadruple = n -> n.double().double()

    123456789
        |> quadruple
        |> ::toString
        |> highlight
        |> println

    var map = HashMap(
        "abc": 123,
        "def": 321,
        "ghi": 987,
    )
    map.forEach((key, value) ->
        println(`{key}: {value}`)
    )

    /*
     * Sylan does not allow scope shadowing. As functions are just
     * variables of a "function type", `factorial` would clash with the
     * top-level `factorial` function defined later on, hence the disambiguating
     * name `innerFactorial`.
     */
    var innerFactorial = for var n = 20, var result = 1 {
        if n <= 0 {
            result
        } else {
            continue n - 1, n * result
        }
    }

    OUTER: for var n = 10 {
        if 0 < n {
            for {

                // A `for` with no clauses is an infinite loop, except for the
                // use of a continue label here that lets it jump to an outer
                // loop.
                OUTER: continue n - 1
            }
        }
    }

    println(`factorial: {innerFactorial}`)
}


/*
 * All variables (including functions), packages, classes, getters, fields,
 * and methods are private unless explicitly opened up with the `public`
 * keyword. If the developer wants to expose them openly but only to
 * its own module, they can use the `internal` keyword.
 *
 * As can be seen in this function and the previous, Sylan allows functions to
 * stand alone in packages without being tied to classes or interfaces.
 */

internal int factorial(int n) {
    switch n {
        0, 1 ->
            1
        n if n < 0 ->
            throw Exception("n cannot be less than 0")
        n ->
            /*
             * Guaranteed tail call elimination will ensure a stack overflow
             * does not occur.
             */
            factorial(n * (n - 1))
    }
}

ignorable internal int printFactorial(int n) {
    /*
     * Sylan does not allow callers to throw away non-`void` function or methods
     * results unless they're declared with the `ignorable` modifier.
     *
     * This makes sense when getting a return value is potentially useful but
     * not the entire reason behind invoking it.  Such a function or method can
     * be assumed to have side effects, with the exception of constant-time
     * operation NOPs or the like.
     */

    var result = factorial(n)
    println(result)

    /*
     * Sylan is an expression-oriented language, so everything returns a value
     * including loops and `if`s. Therefore, the last value is returned by
     * default. The lack of explicit `return`s mean there is always a single
     * exit point from a function or method: the last expression.
     */
    result
}

private void twice<N>(N n, N f(N)) where
    N : Number
{
    f(n) + f(n)
}

String double<N>(N n) where
    N : Add & ToString
{
    (n + n).toString()
}

Optional<int> demoContexts() {

    /*
     * Haskell programmers will recognise this as a monadic do notation.
     * Non-Haskell programmers will be glad to know that this otherwise esoteric
     * feature can make tangled code such as optionality-checking chains and
     * validation code much cleaner.
     */
    do {
        var a <- Some(5)
        doSomething()
        var b <- Empty()
        willNotBeRun()
    }
}

package counter {

    enum Message {
        Increment,
        Reset(int),
        Get,
    }

    public void start(Task sender, int n = 0) {
        select Message {
            .Increment:
                start(sender, n + 1)
            .Reset(n):
                start(sender, n)
            .Get:
                sender.send(n)
                start(sender, n)
            timeout 10.seconds:
                throw Exception("timed out!")
        }
    }
}

interface Concatenate<T, Result = T> {
    public Result concatenate(T y)
}

interface Equals<T = Self> {
    public boolean equals(T other)

    /*
     * Sylan's interface methods can have bodies like Java 8's defender methods.
     * Unlike Java 8, private methods with bodies are also allowed with the
     * `default` keyword. Similar to C# but unlike Java, a default method body
     * in an interface can only be overridden if annotated with `virtual`.
     * `virtual` is not necessary for non-default methods since the lack of
     * concrete inheritance in Sylan means there is no other possible use case
     * for them except to be implemented by the implementor.
     */
    public virtual notEquals(T other) default {
        !equals(other)
    }
}

class Name {
    /**
     * Getters, hashing, equality checking, and the constructor all just exist
     * automatically. They can still be manually overridden if necessary though.
     */

    String firstName
    String lastName
}

class Account implements ToString, Concatenate<Account> {
    /**
     * Classes can implement interfaces but cannot extend other classes.
     * Interfaces can extend other interfaces though. Concrete inheritance
     * generally causes more problems that it's worth and type embedding plus a
     * more powerful interface system makes up for the difference.
     */

    /*
     * This just picks up a class and embeds its contents them into another
     * class. Clashes with existing fields are not tolerated, causing a
     * compilation error.
     *
     * Embedded classes can implement methods for implementing interfaces on its
     * behalf, but it will refuse to clash with existing methods defined in the
     * embedding class itself.
     *
     * Note that no relationship whatsoever is established between the classes
     * after the embedding; there is no concrete subtyping in Sylan. It's solely
     * for DRY: don't repeat yourself.
     */
    embed Name

    int ageInYears

    ZonedDateTime _expiry = ZonedDateTime.now() + 1.year

    public Account(String firstName, String lastName) {
        println("instantiating an Account...")
        super(.firstName, .lastName, ageInYears = 35)
    }

    public override String toString() {
        `{firstName} {lastName} is {ageInYears} years old`
    }

    public override Account concatenate(Account a) {
        var firstName = firstName.concat(a.firstName)
        var lastName = lastName.concat(a.lastName)

        Account(
            .firstName,
            .lastName,
            ageInYears = ageInYears + a.ageInYears,
        )
    }

    /*
     * Getters are defined like methods but look like fields. Setters don't
     * exist because Sylan is an immutable language.
     */
    public String get name {
        `{firstName} {lastName}`
    }

    public boolean get locked {
        expiry < ZonedDateTime.now
    }
}

extend class Account implements Concatenate<Account, Result = String> {

    public override String concatenate(This that) {
        `{firstName} {that.firstName}`
    }
}

void demoPatternMatching() {
    var account1 = Account(firstName = "Tom", lastName = "Smith")
    var matchingLastName = "Smith"

    if var Account(firstName, lastName = .matchingLastName) as account
            = account1 {
        println(`Matching first name: {firstName}`)
        println(`Matching account: {account}`)
    }

    switch account1 {

        Account(locked = true), Account(firstName = "unlucky") ->
            println("LOCKED!")

        Account(expiry) if expiry < ZonedDateTime.now ->
            println("ALSO LOCKED")

        default {
            println("NOT LOCKED: ")
            println(account1)
        }

        /*
         * Switches are expressions, support multiple cases, can do pattern
         * matching, and have support for conditional "guards".
         */
    }
}

/*
 * Aliasing of various constructs to allow easy code repair during large-scale
 * refactoring. Any item in any module can be aliased, which can ease module
 * versioning where new major module versions are effectively different modules.
 */
alias counter2 = counter
alias Person = Account
alias Showable = ToString
alias factorial2 = factorial

int maxBound = 5

void closureDemo() {
    var x = 5

    var account1 = Account(
        firstName = "Tom",
        lastName = "Smith",
        ageInYears = 15,
    )

    var firstName = "Tom"
    var lastName = "Smith"
    var age = 25
    var account2 = Account(.firstName, .lastName, ageInYears = age)

    var f = a ->
        println(a.toString())

    f(account1)
    f(account2(firstName = "Emma"))

    var g = a -> {
        println("returning an account")
        a
    }

    var z = g(account1)

    var n = twice(3, x -> x * 2)
    println(`n == {n}`)
}

void demoNumericLiterals() {
    int a = 5
    uint b = 5u
    decimal c = 10.0

    byte d = 5u8
    uint16 e = 11u16
    uint32 f = 12u32
    uint64 g = 13u64
    int8 h = 15s8
    short i = 13s16
    int32 j = 7s32
    long k = 7s64
    float l = 12f16
    double m = 8f32
}

/*
 * Top-level code is allowed, but only in the main package within the main
 * module. Code in other packages must be in functions or methods.
 */

var optionalString = Some("test string")
if var Some(s) = optionalString {
    println(s)
}

var c = Task(-> counter.start(currentTask))
5.times(-> c.send(counter.Message.Increment()))

c.send(counter.Message.Get())
c.send(counter.Message.Increment())
c.send(counter.Message.Get())

// Should print 5 and then 6.
2.times(->
    select int n {
        println(`{n}`)
    }
)

print("""
Multiline
strings
""")

var x = {
    println("Returning 5 to be bound as x...")
    5
}
print(`{x}`)

/*
  A multiline comment.

  /*
    A nested multiline comment.
  */
*/
